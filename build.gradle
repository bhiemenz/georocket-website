plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id "com.moowork.gulp" version "0.12"
    id "com.moowork.node" version "0.12"
}

apply plugin: 'org.asciidoctor.convert'

import org.apache.tools.ant.filters.ConcatFilter

ext {
    docsSources = new File(buildDir, "docs-sources")
    docs = new File(buildDir, "docs")
    src = new File("src")
    srcGen = new File(buildDir, "src-gen")
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations {
    docs {
        transitive = false
    }
}

dependencies {
    docs "io.georocket:georocket-docs:1.0.0-SNAPSHOT:sources"
    docs "io.georocket:georocket-client-api:1.0.0-SNAPSHOT:javadoc"
    docs "io.georocket:georocket-server-api:1.0.0-SNAPSHOT:javadoc"
}

gulp {
    colors = true
}

node {
    version = '6.1.0'
    download = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task extractDocs << {
    // extract each 'docs' artifact to a separate directory
    configurations.docs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        copy {
            from zipTree(artifact.file)
            into new File(new File(docsSources, artifact.name), artifact.moduleVersion.id.version)
        }
    }
}

task copyDocs(dependsOn: [extractDocs, asciidoctor]) << {
    copy {
        from new File(asciidoctor.outputDir, "html5")
        into new File(srcGen, "georocket-docs-html")
    }
    copy {
        from new File(docsSources, "georocket-client-api")
        into new File(docs, "api/client")
    }
    copy {
        from new File(docsSources, "georocket-server-api")
        into new File(docs, "api/server")
    }
}

task prepareUserDocs(dependsOn: [copyDocs]) << {
    def docsheader = new File(srcGen, "docsheader.txt")
    docsheader.text = "---\ntemplate: page.html\ntitle: User documentation\n---\n\n"
    copy {
        from new File(srcGen, "georocket-docs-html")
        into new File(src, "docs/user-documentation")
        exclude "*.html"
    }
    copy {
        from new File(srcGen, "georocket-docs-html")
        into new File(src, "docs/user-documentation")
        include "*.html"
        filter(ConcatFilter, prepend: docsheader)
    }
}

asciidoctor {
    sourceDir new File(docsSources, "georocket-docs/1.0.0-SNAPSHOT")
    options header_footer: false
}

asciidoctor.dependsOn(extractDocs)

gulp_build.dependsOn copyDocs, prepareUserDocs, npmInstall

assemble.dependsOn gulp_build

defaultTasks "build"
